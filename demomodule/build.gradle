def currentModule = rootProject.modules.demomodule
if (currentModule.isStandalone) {
//apply plugin: 'com.jakewharton.butterknife'
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
android {
    compileSdkVersion 26
    resourcePrefix "demo_"
    dataBinding {
        enabled true
    }

    defaultConfig {
        if (currentModule.isStandalone) {
            applicationId currentModule.applicationId
            minSdkVersion 15
            targetSdkVersion 26
            versionCode 1
            versionName "1.0"

            testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        }

        def moduleName = project.getName()
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: moduleName]
            }
        }
    }
    sourceSets {
        main {
            if (currentModule.isStandalone) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug*'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    implementation project(':commonlib')

    annotationProcessor 'com.alibaba:arouter-compiler:1.1.4'
    //依赖注入 编译需要
    annotationProcessor rootProject.ext.dependencies.dagger2_compiler
}
